############################
### CPACK CONFIG + Qt ITW ##
############################

if (APPLE)
    INSTALL(TARGETS ${PROJECT_NAME} DESTINATION .)
endif (APPLE)

if (WIN32)
    INSTALL(DIRECTORY ${CMAKE_BINARY_DIR}/bin/ DESTINATION .)
endif (WIN32)

SET(CPACK_MONOLITHIC_INSTALL 1)

SET(FEEDTNZ_DESCRIPTION "Companion app for WTNZ")
SET(FEEDTNZ_DESCRIPTION_LOCALIZED ${FEEDTNZ_DESCRIPTION}
    fr "Logiciel compagnon pour WTNZ"
)

SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY ${FEEDTNZ_DESCRIPTION})
SET(CPACK_IFW_PACKAGE_PUBLISHER "LVWL")
SET(CPACK_IFW_PRODUCT_URL "https://zonme.to2x.ovh/wtnz")
SET(CPACK_IFW_TARGET_DIRECTORY "@ApplicationsDirX64@/FeedTNZ")
SET(CPACK_PACKAGE_ICON ${CMAKE_CURRENT_SOURCE_DIR}/resources/icons/feedtnz.png)
SET(CPACK_IFW_PACKAGE_LOGO ${CMAKE_CURRENT_SOURCE_DIR}/resources/icons/feedtnz.png)

if (APPLE)
    SET(CPACK_IFW_ROOT "/Qt/QtIFW-3.0.6")
    SET(FEEDTNZ_REMOTE_SERVER_PATH "/Volumes/www/feedtnz")
    SET(FEEDTNZ_REMOTE_SERVER_PLATFORM_FOLDER "osx")
    SET(FEEDTNZ_INSTALLER_EXTENSION ".dmg")
endif (APPLE)

if (WIN32)
    SET(CPACK_IFW_ROOT "C:/Qt/QtIFW-3.0.6")
    SET(FEEDTNZ_REMOTE_SERVER_PATH "//192.168.0.12/www/feedtnz")
    SET(FEEDTNZ_REMOTE_SERVER_PLATFORM_FOLDER "win")
    SET(FEEDTNZ_INSTALLER_EXTENSION ".exe")
endif (WIN32)

SET(FEEDTNZ_REMOTE_SERVER_DOWNLOAD_PATH ${FEEDTNZ_REMOTE_SERVER_PATH}/downloads/${FEEDTNZ_REMOTE_SERVER_PLATFORM_FOLDER})
SET(FEEDTNZ_REMOTE_SERVER_PACKAGES_PATH ${FEEDTNZ_REMOTE_SERVER_PATH}/packages/${FEEDTNZ_REMOTE_SERVER_PLATFORM_FOLDER})

SET(CPACK_GENERATOR IFW)
INCLUDE(CPack)
INCLUDE(CPackIFW)

cpack_add_component(${PROJECT_NAME}
                    DISPLAY_NAME "FeedTNZ"
                    DESCRIPTION ${FEEDTNZ_DESCRIPTION}
                    REQUIRED
)

cpack_ifw_configure_component(${PROJECT_NAME} 
                    ESSENTIAL
                    FORCED_INSTALLATION
                    NAME "com.lvwl.feedtnz.core"
                    DESCRIPTION ${FEEDTNZ_DESCRIPTION_LOCALIZED}
                    DEFAULT TRUE
)

cpack_ifw_add_repository(coreRepo URL "https://zonme.to2x.ovh/feedtnz/packages/${FEEDTNZ_REMOTE_SERVER_PLATFORM_FOLDER}")

#############
## Publish ## 
#############

SET(FEEDTNZ_PACKAGE_FILE_NAME ${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-${CPACK_SYSTEM_NAME})
SET(FEEDTNZ_PACKAGED_PATH ${CMAKE_BINARY_DIR}/_CPack_Packages/${CPACK_SYSTEM_NAME}/IFW/${FEEDTNZ_PACKAGE_FILE_NAME})
SET(FEEDTNZ_PACKAGED_INSTALLER_PATH ${FEEDTNZ_PACKAGED_PATH}${FEEDTNZ_INSTALLER_EXTENSION})
SET(FEEDTNZ_PACKAGED_REPOSITORY_PATH ${FEEDTNZ_PACKAGED_PATH}/repository)

#install CoreUtils for Win32 if mv missing
add_custom_target(publishPackage 
    COMMAND ${CMAKE_COMMAND} --build . --config Release --target package
    COMMAND cp -rf ${FEEDTNZ_PACKAGED_INSTALLER_PATH} ${FEEDTNZ_REMOTE_SERVER_DOWNLOAD_PATH}
    COMMAND cp -rf ${FEEDTNZ_PACKAGED_REPOSITORY_PATH}/* ${FEEDTNZ_REMOTE_SERVER_PACKAGES_PATH}
)